buildscript {

  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url 'https://maven.google.com/'
    }
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-allopen:1.3.21"
    classpath "org.jacoco:org.jacoco.core:0.8.3"
  }
}

evaluationDependsOn(':plexapp-rest-library')
evaluationDependsOn(':subtitle-converter')

repositories {
  google()
  jcenter()
  mavenCentral()
  maven {
    url "http://kingargyle.github.io/repo"
  }
  maven {
    url 'https://maven.google.com/'
  }
  maven { url 'https://jitpack.io' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-allopen"
apply plugin: "jacoco"
apply plugin: 'org.sonarqube'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

sonarqube {
  properties {
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.organization", "kingargyle-github"
    property "sonar.login", "cb7af08f0b0e86306ac5365074326dc27aba503e"
    property "sonar.jacoco.xmlReportPath", "${project.buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
    property "sonar.coverage.exclusions", "**/R.class, **/R\$*.class, **/BuildConfig.*, **/Manifest*.*, **/*Test*.*,android/**/*.*, **/*MemberInjector.*, **/*\$\$Factory*.*, **/*..State*.*, **/*_ViewBinding*.*"
  }
}

allOpen {
  annotation("us.nineworlds.serenity.common.annotations.OpenForTesting")
}

android {
  defaultConfig {
    versionCode 2010000
    versionName "2.1.0-M2"

    minSdkVersion 21
    targetSdkVersion 28
    multiDexEnabled true
    multiDexKeepProguard file("multidex_keep_file.txt")

//    javaCompileOptions {
//      annotationProcessorOptions {
//        arguments = [ toothpick_registry_package_name : "us.nineworlds.serenity",
//                      toothpick_registry_children_package_names : "us.nineworlds.serenity.emby"]
//      }
//    }

  }

  aaptOptions {
    cruncherEnabled false
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }

  compileSdkVersion 28
  buildToolsVersion '28.0.2'

  if (project.hasProperty("keystore")) {
    signingConfigs {
      release {
        storeFile file(project.property("keystore"))
        storePassword project.property("storepass")
        keyAlias project.property("keyalias")
        keyPassword project.property("keypass")
      }
    }
  }

  targetCompatibility = '1.8'
  sourceCompatibility = '1.8'

  buildTypes {
    debug {
      minifyEnabled false
      testCoverageEnabled true
    }
    release {
      if (project.hasProperty("keystore")) {
        signingConfig signingConfigs.release
      }
      zipAlignEnabled true
      minifyEnabled false
    }
  }

  lintOptions {
    checkReleaseBuilds false
    // Or, if you prefer, you can continue to check for errors in release builds,
    // but continue the build even when errors are found:
    abortOnError false
  }

  testOptions {
    unitTests.returnDefaultValues = true
    unitTests.includeAndroidResources = true
    unitTests.all {
      minHeapSize = "512m"
      maxHeapSize = "1024m"

      jvmArgs '-noverify'
    }
  }

  compileOptions {
    incremental = false
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }

  dexOptions {
    preDexLibraries = false
  }
}

afterEvaluate {
  tasks.matching {
    it.name.startsWith('dex')
  }.each { dx ->
    if (dx.additionalParameters == null) {
      dx.additionalParameters = ['--multi-dex']
    } else {
      dx.additionalParameters += '--multi-dex'
    }
  }
}

dependencies {

  testImplementation("org.robolectric:robolectric:$robolectricVersion") {
    exclude module: 'support-v4'
  }
  testImplementation("com.squareup.assertj:assertj-android:$assertJAndroidVersion") {
    exclude module: 'support-v4'
    exclude module: 'support-annotations'
  }

  testImplementation "org.assertj:assertj-core:$assertjVersion"
  testImplementation "junit:junit:$junitVersion"
  testImplementation "org.robolectric:shadows-supportv4:$robolectricVersion"
  testImplementation "org.robolectric:shadows-framework:$robolectricVersion"
  testImplementation "org.robolectric:shadowapi:$robolectricVersion"
  testImplementation "org.robolectric:shadows-playservices:$robolectricVersion"
  testImplementation "androidx.test:core:1.1.0"
  testImplementation "org.khronos:opengl-api:$openglApiVersion"
  testImplementation "org.mockito:mockito-core:$mockitoVersion"


  implementation project(':plexapp-rest-library')
  implementation project(':subtitle-converter')
  implementation project(':emby-lib')
  implementation project(':serenity-android-common')
  implementation project(':serenity-common')

  implementation 'com.google.firebase:firebase-core:16.0.6'
  implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'

  implementation group: 'com.github.bumptech.glide', name: 'okhttp3-integration', version: "$glideOkHttpVersion"
  implementation("com.google.android.exoplayer:exoplayer-core:$exoplayerVersion") {
    exclude module: 'support-annotations'
  }
  implementation("com.google.android.exoplayer:exoplayer-ui:$exoplayerVersion") {
    exclude module: 'support-annotations'
  }

  implementation("com.google.android.exoplayer:extension-okhttp:$exoplayerVersion") {
    exclude module: 'support-annotations'
  }

  implementation("com.squareup.okhttp3:okhttp:$okhttpVersion") {
    exclude group: 'com.android.support'
  }

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
  implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
  implementation "com.arello-mobile:moxy:$moxyVersion"
  implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"
  implementation "com.github.bumptech.glide:glide:$glideVersion"
  implementation "org.greenrobot:eventbus:$eventBus"
  implementation "com.birbit:android-priority-jobqueue:$androidPriorityJobQueueVersion"
  implementation "com.android.support:appcompat-v7:$androidSupportLibVersion"
  implementation "com.android.support:leanback-v17:$androidSupportLibVersion"
  implementation "com.android.support:preference-leanback-v17:$androidSupportLibVersion"
  implementation "com.android.support:support-v4:$androidSupportLibVersion"
  implementation "com.googlecode.juniversalchardet:juniversalchardet:$universalCharDetVersion"
  implementation "com.mcxiaoke.volley:library:$volleyVersion"
  implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
  implementation "me.jessyan:retrofit-url-manager:$urlManager"
  implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttpVersion"
  //TODO Upgrade to Android X and upgrade to 10.0.0
  implementation "com.jakewharton:butterknife:$butterKnifeVersion"
  implementation "com.jakewharton.timber:timber:$timberVersion"
  implementation "com.android.support:cardview-v7:$androidSupportLibVersion"
  implementation "com.android.support:support-annotations:$androidSupportLibVersion"

  releaseImplementation("com.github.stephanenicolas.toothpick:toothpick-runtime:$toothPickVersion") {
    exclude group: 'javax.inject'
  }
  releaseImplementation("com.github.stephanenicolas.toothpick:smoothie:$toothPickVersion") {
    exclude group: 'javax.inject'
  }

  debugImplementation("com.github.stephanenicolas.toothpick:toothpick-runtime:$toothPickVersion")

  releaseImplementation "com.github.stephanenicolas.toothpick:toothpick-javax-annotations:$toothPickVersion"
  kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:$toothPickVersion"

  implementation "com.squareup.moshi:moshi-kotlin:$moshiKotlinVersion"
  implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
  implementation "net.danlew:android.joda:$jodaTimeVersion"
  implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
  //TODO Upgrade to Android X and update to 1.1.0
  implementation 'com.google.android:flexbox:1.0.0'
  implementation 'com.henryblue.library:tvrecyclerview:1.2.2'
  //TODO Upgrade to Android X and update to 3.0.0
  implementation 'jp.wasabeef:recyclerview-animators:2.3.0'

  implementation('com.github.rstanic12:Resourceful:1.0.0') {
    exclude group: 'com.google.guava'
  }

  // https://mvnrepository.com/artifact/org.simpleframework/simple-xml
  implementation("org.simpleframework:simple-xml:$simpleXmlVersion") {
    exclude group: 'stax'
    exclude group: 'xpp3'
  }

  testImplementation "com.nhaarman:mockito-kotlin:1.6.0"
  testImplementation "org.apache.commons:commons-lang3:$commonsLangVersion"

  testImplementation "com.squareup.okhttp3:mockwebserver:$okhttpVersion"
  testImplementation "commons-io:commons-io:$commonsioVersion"
  testImplementation "com.github.stephanenicolas.toothpick:toothpick-testing:$toothPickVersion"
  kaptTest "com.github.stephanenicolas.toothpick:toothpick-compiler:$toothPickVersion"

  kapt "com.arello-mobile:moxy-compiler:$moxyVersion"
  kapt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
}

com.google.gms.googleservices.GoogleServicesPlugin.config.disableVersionCheck = true

jacoco {
  toolVersion = "0.8.3"
}

tasks.withType(Test) {
  jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {

  reports {
    xml.enabled = true
    html.enabled = true
  }

  def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*MemberInjector.*',
                    '**/*$$Factory*.*', '**/*..State*.*', '**/*_ViewBinding*.*']
  def mainSrc = "${project.projectDir}/src/main/java"
  def kotlinSrc = "${project.projectDir}/src/main/kotlin"

  classDirectories = fileTree(
          dir: "$buildDir/intermediates/javac/debug/compileDebugJavaWithJavac/classes",
          excludes: fileFilter) + fileTree(
          dir: "$buildDir/tmp/kotlin-classes/debug",
          excludes: fileFilter)

  // sources
  sourceDirectories = files([
          mainSrc,
          kotlinSrc
  ])

  executionData = files("${buildDir}/jacoco/testDebugUnitTest.exec")
}
